
mod std

pub fun List::eq[T](lhs: List[T], rhs: List[T]) -> Bool {
    if (lhs |> length()) != (rhs |> length()) { return false }
    return Sequence::zip(lhs |> values(), rhs |> values(), T::eq)
        |> all(|e| e)
}

pub fun List::not_eq[T](lhs: List[T], rhs: List[T]) -> Bool 
    = !List::eq(lhs, rhs)

pub fun List::as_string[T](self: List[T]) -> String {
    val contents = self
        |> values()
        |> map[T, String](T::as_string)
        |> join(", ")
    return "List(" |> concat(contents) |> concat(")")
}

pub fun List::as_hash[T](self: List[T]) -> Int = self
    |> values()
    |> map(T::as_hash)
    |> reduce(hash::combine_ord)
    |> unwrap_or(0)

pub fun List::clone[T](self: List[T]) -> mut List[T] 
    = self |> slice(0, self |> length())

pub fun List::empty[T]() -> mut List[T] = List::of[T]()

pub fun List::of[T](...values: mut List[T]) -> mut List[T] = values

pub fun List::get[T](self: List[T], position: Int) -> Option[T] {
    mut idx = position
    if idx < 0 { idx = (self |> length()) + idx }
    if idx < 0 { return Option::None }
    if idx >= (self |> length()) { return Option::None }
    return Option::Some(self |> get_unchecked(idx))
}

pub fun List::at[T](self: List[T], position: Int) -> T = self
    |> get(position)
    |> unwrap_or_else(|| "Index " 
        |> concat(position |> as_string())
        |> concat(" is out of bounds for a list of range ")
        |> concat(self |> length() |> as_string())
        |> panic()
    )

pub fun List::slice_from[T](self: List[T], start: Int) -> mut List[T]
    = self |> slice(start, self |> length())

pub fun List::slice_to[T](self: List[T], end: Int) -> mut List[T]
    = self |> slice(0, end)

pub fun List::indices[T](self: List[T]) -> mut Sequence[Int] 
    = range(0, self |> length())

pub fun List::indices_rep[T](self: List[T]) -> mut Stream[Int] {
    if 0 == (self |> length()) {
        return panic("Attempt to get index stream from empty list!")
    }
    return count_from(0)
        |> map(|i| i % (self |> length()))
}

pub fun List::values[T](self: List[T]) -> mut Sequence[T]
    = range(0, self |> length())
    |> map[Int, T](|i| self |> get_unchecked(i % (self |> length())))

pub fun List::values_rep[T](self: List[T]) -> mut Stream[T] {
    if 0 == (self |> length()) {
        return panic("Attempt to get element stream from empty list!")
    }
    return count_from(0)
        |> map[Int, T](|i| self |> get_unchecked(i % (self |> length())))
}

pub fun List::reversed[T](self: List[T]) -> mut Sequence[T]
    = range_incl(1, self |> length())
    |> map[Int, T](|i| self |> get_unchecked((self |> length()) - i))

pub fun List::collect[T](from: mut Sequence[T]) -> mut List[T] {
    val r = List::of[T]()
    from |> for_each(|v| {
        r |> push(v)
    })
    return r
}

pub fun List::is_empty[T](self: List[T]) -> Bool = (self |> length()) == 0

pub fun List::remove[T](self: mut List[T], position: Int) -> T = self 
    |> splice(position, position + 1) 
    |> get_unchecked(0)

pub fun List::pop[T](self: mut List[T]) -> Option[T] {
    val l = self |> length()
    if l == 0 { return Option::None }
    return Option::Some(self |> splice(l - 1, l) |> get_unchecked(0))
}

pub fun List::clear[T](self: mut List[T]) {
    self |> splice(0, self |> length())
}