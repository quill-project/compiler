
mod std

struct Sequence[T](next: Fun() -> Option[T])

pub fun Sequence::new[T](next: Fun() -> Option[T]) -> Sequence[T] 
    = Sequence(next)

pub fun Sequence::of[T](value: T) -> Sequence[T] {
    mut had_value = false
    return Sequence(|| {
        if had_value { return Option::None }
        had_value = true
        return Option::Some(value)
    })
}

pub fun Sequence::empty[T]() -> Sequence[T] 
    = Sequence::new(|| Option::None)

pub fun Sequence::chain[T](first: Sequence[T], after: Sequence[T]) -> Sequence[T]
    = Sequence::new(|| first |> next() |> or_else(|| after |> next()))

pub fun Sequence::zip[A, B, O](a: Sequence[A], b: Sequence[B], f: Fun(A, B) -> O) -> Sequence[O] 
    = Sequence::new(|| a 
        |> next() 
        |> and_then[A, O](|x| b 
            |> next() 
            |> map[B, O](|y| f(x, y))
        ) 
    )

pub fun Sequence::map[I, O](self: Sequence[I], f: Fun(I) -> O) -> Sequence[O]
    = Sequence::new(|| self |> next() |> map(f))

pub fun Sequence::skip[T](self: Sequence[T], n: Int) -> Sequence[T] {
    self |> take(n) |> for_each(|v| {})
    return self
}

pub fun Sequence::take[T](self: Sequence[T], n: Int) -> Sequence[T] {
    mut i = 0
    return Sequence::new(|| {
        if i >= n { return Option::None }
        i = i + 1
        return self |> next()
    })
}

pub fun Sequence::take_while[T](self: Sequence[T], cond: Fun(T) -> Bool) -> Sequence[T] {
    mut ended = false
    return Sequence::new(|| {
        if ended { return Option::None }
        match self |> next() {
            None { return Option::None }
            Some(n) {
                ended = !cond(n)
                return if ended { Option::None[T] } else { Option::Some(n) }
            }
        }
    })
}

pub fun Sequence::take_until[T](self: Sequence[T], cond: Fun(T) -> Bool) -> Sequence[T] {
    mut ended = false
    return Sequence::new(|| {
        if ended { return Option::None }
        match self |> next() {
            None { return Option::None }
            Some(n) {
                ended = cond(n)
                return if ended { Option::None[T] } else { Option::Some(n) }
            }
        }
    })
}

pub fun Sequence::flatten[T](self: Sequence[Sequence[T]]) -> Sequence[T] {
    mut inner: Option[Sequence[T]] = Option::None
    return Sequence::new(|| {
        mut value: Option[T] = Option::None
        Sequence::new[Unit](|| {
            if value |> is_some() { return Option::None }
            inner |> map[Sequence[T], Unit](|i| {
                value = i |> next()
                return unit
            })
            if value |> is_some() { return Option::None }
            inner = self |> Sequence::next()
            return inner |> map[Sequence[T], Unit](|_| unit)
        }) |> for_each(|_| {})
        return value
    })
}

pub fun Sequence::length[T](self: Sequence[T]) -> Int {
    mut c = 0
    self |> for_each(|_| {
        c = c + 1
    })
    return c
}

pub fun Sequence::next[T](self: Sequence[T]) -> Option[T] = self.next()

pub fun Sequence::last[T](self: Sequence[T]) -> Option[T] {
    mut v = Option::None[T]
    self |> for_each(|value| {
        v = Option::Some(value)
    })
    return v
}

pub fun Sequence::find[T](self: Sequence[T], cond: Fun(T) -> Bool) -> Option[T]
    = self |> filter(cond) |> next()

pub fun Sequence::find_last[T](self: Sequence[T], cond: Fun(T) -> Bool) -> Option[T]
    = self |> filter(cond) |> last()

pub fun Sequence::any[T](self: Sequence[T], cond: Fun(T) -> Bool) -> Bool
    = self |> filter(cond) |> next() |> is_some()

pub fun Sequence::all[T](self: Sequence[T], cond: Fun(T) -> Bool) -> Bool
    = self |> filter(|e| !cond(e)) |> next() |> is_none()

pub fun Sequence::fold[I, O](self: Sequence[I], start: O, f: Fun(O, I) -> O) -> O {
    mut acc = start
    self |> for_each(|c| {
        acc = f(acc, c)
    })
    return acc
}

pub fun Sequence::reduce[T](self: Sequence[T], f: Fun(T, T) -> T) -> Option[T]
    = self |> next() |> map(|start| self |> fold(start, f))

pub fun Sequence::filter[T](self: Sequence[T], cond: Fun(T) -> Bool) -> Sequence[T] 
    = Sequence::new(|| {
        mut found = self |> next()
        Sequence::new[Unit](|| {
            match found {
                None { return Option::None }
                Some(value) {
                    if cond(value) { return Option::None }
                    found = self |> next()
                    return Option::Some
                }
            }
        }) |> for_each(|n| {})
        return found
    })

pub fun Sequence::for_each[T](self: Sequence[T], f: Fun(T))
    = self |> for_each_until(f, Option::None)
